import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
    ext {
        springBootVersion = "2.2.0.RELEASE"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

plugins {
    id "java"
    id "org.flywaydb.flyway" version "5.2.4"
    id "com.gorylenko.gradle-git-properties" version "1.5.1"
}

apply plugin: "org.springframework.boot"

springBoot {
    buildInfo()
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    compile "mysql:mysql-connector-java:8.0.18"

    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testCompile "org.mockito:mockito-core:2.23.4"
}

def developmentDbUrl = "jdbc:mysql://localhost:3306/tracker_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
bootRun {
    environment("WELCOME_MESSAGE", "hello")
    environment("SPRING_DATASOURCE_URL", developmentDbUrl)
    environment("MANAGEMENT_ENDPOINTS_WEB_EXPOSURE_INCLUDE", "*")
    environment("MANAGEMENT_ENDPOINT_HEALTH_SHOWDETAILS", "always")
}

def testDbUrl = "jdbc:mysql://localhost:3306/tracker_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
test {
    environment("WELCOME_MESSAGE", "Hello from test")
    environment("SPRING_DATASOURCE_URL", testDbUrl)
    environment("MANAGEMENT_ENDPOINTS_WEB_EXPOSURE_INCLUDE", "*")
    environment("MANAGEMENT_ENDPOINT_HEALTH_SHOWDETAILS", "always")
}

flyway {
    url = developmentDbUrl
    user = "tracker"
    password = ""
    locations = ["filesystem:databases/tracker/migrations"]
}

task testMigrate(type: FlywayMigrateTask) {
    url = testDbUrl
}